apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.zegoggles.smssync"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 1576
        versionName "1.5.11"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig keystoreProperties.isEmpty() ? null : signingConfigs.release
        }
    }

    lintOptions {
        // http://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.LintOptions.html#com.android.build.gradle.internal.dsl.LintOptions
        warningsAsErrors true
    }

    //  avoid "Duplicate files copied in APK META-INF/"
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.squareup:otto:1.3.8'
    implementation 'com.github.jberkel.k-9:k9mail-library:eaf689025e'
    implementation 'com.github.jberkel.whassup:library:0.0.7@jar'
    implementation 'com.android.billingclient:billing:1.0'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:preference-v7:27.1.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.6.1'
    testImplementation 'com.google.truth:truth:0.39'
    testImplementation 'org.mockito:mockito-all:1.10.17'
}

tasks.withType(JavaCompile) {
    if (!it.name.toLowerCase().contains('test')) {
        options.compilerArgs = ['-Xlint:unchecked', '-Xlint:deprecation']
    }
}
